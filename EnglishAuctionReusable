// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/utils/math/SafeMath.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/token/ERC721/utils/ERC721Holder.sol";

contract EnglishAuction is ERC721Holder, ReentrancyGuard {
    using SafeMath for uint256;
    address payable public immutable owner;
    address payable public immutable creator;
    uint256 public immutable royaltyPercentage;
    uint256 public start;
    uint256 public end;
    uint256 public currentHighestBid;
    uint256 public minimumBid;
    address public currentHighestBidder;
    bool public ended;
    IERC721 public nft;
    uint256 public tokenId;
    bool private _hasFinalized;

    uint256 private constant PERCENTAGE_DIVISOR = 10000;

    event NewHighestBid(address indexed bidder, uint256 amount);
    event BidOutbid(address indexed outbidBidder, uint256 newHighestBid);
    event AuctionEnded(address indexed winner, uint256 amount);
    event AuctionFinalized(address indexed winner, uint256 amount);
    event NFTTransferred(address indexed from, address indexed to, uint256 indexed tokenId);
    event AuctionReset(uint256 indexed tokenId, uint256 start, uint256 end, uint256 minimumBid);

    constructor (
        uint256 _royaltyPercentage,
        uint256 _start,
        uint256 _end,
        uint256 _minimumBid,
        address _nftAddress,
        uint256 _tokenId,
        address payable _creator
    ) {
        require(_start > block.timestamp, "Start time must be in the future.");
        require(_end > _start, "End time must be after start time.");

        owner = payable(msg.sender);
        creator = _creator;
        royaltyPercentage = _royaltyPercentage;
        start = _start;
        end = _end;
        nft = IERC721(_nftAddress);
        tokenId = _tokenId;
        minimumBid = _minimumBid;
    }

    modifier onlyNotTransferred() {
        require(nft.ownerOf(tokenId) != address(this), "NFT already transferred");
        _;
    }

    function transferNFT() external onlyOwner onlyNotTransferred {
        address from = nft.ownerOf(tokenId);
        nft.transferFrom(from, address(this), tokenId);
        emit NFTTransferred(from, address(this), tokenId);
    }

    modifier onlyOwner() {
        require(msg.sender == owner, "Caller is not the auction owner");
        _;
    }

    modifier auctionEnded() {
        require(block.timestamp >= end, "Auction is still ongoing");
        _;
    }

    modifier onlyBefore(uint _time) {
        require(block.timestamp < _time, "Operation not allowed after the specified time");
        _;
    }

    modifier onlyNotOwner() {
        require(msg.sender != owner, "Operation not allowed for the contract owner");
        _;
    }

        function placeBid() external payable nonReentrant onlyBefore(end) onlyNotOwner {
        require(msg.sender != address(0), "Bidder address must not be the zero address");
        require(msg.value >= minimumBid, "Bid too low.");
        require(msg.value > currentHighestBid, "Bid too low.");

        uint256 refundAmount = 0;
        if (currentHighestBid != 0) {
            refundAmount = currentHighestBid;
            payable(currentHighestBidder).transfer(refundAmount);
        }

        emit BidOutbid(currentHighestBidder, msg.value);

        currentHighestBid = msg.value;
        currentHighestBidder = msg.sender;

        emit NewHighestBid(msg.sender, msg.value);
    }

   function endAuction() external onlyOwner {
        require(nft.ownerOf(tokenId) == address(this), "NFT not transferred to contract");
        require(!ended, "Auction has already ended.");
        require(block.timestamp >= end, "Auction has not ended yet.");

        ended = true;

        emit AuctionEnded(currentHighestBidder, currentHighestBid);
    }

    function onERC721Received(
        address,
        address,
        uint256,
        bytes memory
    ) 
    public pure override returns (bytes4) {
        return this.onERC721Received.selector;
    }

    function getCurrentHighestBid() public view returns (uint256) {
        return currentHighestBid;
    }

    function getCurrentHighestBidder() public view returns (address) {
        return currentHighestBidder;
    }

    function getAuctionEndTime() public view returns (uint256) {
        return end;
    }

    function finalize() public onlyOwner nonReentrant auctionEnded {
        require(nft.ownerOf(tokenId) == address(this), "Contract does not hold the NFT");
        require(!_hasFinalized, "Auction has already been finalized.");
        require(ended, "Auction has not ended yet.");

        // Calculate the royalty amount
        uint256 royaltyAmount = currentHighestBid.mul(royaltyPercentage).div(PERCENTAGE_DIVISOR);

        // Send the royalty amount to the creator
        (bool successRoyalty, ) = creator.call{value: royaltyAmount}("");
        require(successRoyalty, "Transfer to creator failed.");

        // Calculate the remaining total
        uint256 remainingTotal = currentHighestBid - royaltyAmount;

        // Send remaining funds to owner
        (bool successOwner, ) = owner.call{value: remainingTotal}("");
        require(successOwner, "Transfer to owner failed.");

        // Set hasFinalized to true
        _hasFinalized = true;

        // Transfer the NFT to the winner
        nft.safeTransferFrom(address(this), currentHighestBidder, tokenId);

        emit AuctionFinalized(currentHighestBidder, currentHighestBid);
    }

    function withdrawExcessFunds() external onlyOwner {
        require(_hasFinalized, "Auction has not been finalized yet.");
        uint256 remainingBalance = address(this).balance;
        require(remainingBalance > 0, "No remaining funds to withdraw.");
        owner.transfer(remainingBalance);
    }

        function resetAuction(uint256 _start, uint256 _end, uint256 _minimumBid, uint256 _tokenId) external onlyOwner {
        require(_hasFinalized, "Auction has not been finalized yet.");
        require(_start > block.timestamp, "Start time must be in the future.");
        require(_end > _start, "End time must be after start time.");

        emit AuctionReset(_tokenId, _start, _end, _minimumBid);

        start = _start;
        end = _end;
        currentHighestBid = 0;
        minimumBid = _minimumBid;
        currentHighestBidder = address(0);
        ended = false;
        tokenId = _tokenId;
        _hasFinalized = false;
    }
}
